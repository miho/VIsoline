buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath fileTree (dir: 'lib/', includes: ['*.jar'])
    }
}

plugins {
    id "com.github.hierynomus.license" version "0.13.1"
    id 'maven-publish'
    id 'net.nemerosa.versioning' version '2.4.0'
    id 'com.jfrog.bintray' version '1.7.3'
    id 'com.github.ben-manes.versions' version '0.13.0'
}

apply plugin: 'eclipse'
apply plugin: 'java'
apply from: 'gradle/publishing.gradle'

task wrapper(type: Wrapper) {
    gradleVersion = '4.1'
    distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}

sourceCompatibility = '1.8'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }
}

// JDK 8 is way too strict about Javadoc style
if (JavaVersion.current().isJava8Compatible()) {
    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.10'
}


if (!project.plugins.findPlugin(eu.mihosoft.g4j.gradle.G4JPlugin)) {
    project.apply(plugin: eu.mihosoft.g4j.gradle.G4JPlugin)
}

g4j.g4jPackageName = "eu.mihosoft.visoline"

compileJava.doFirst {
    tasks.g4j.execute()
}

license {
    header = rootProject.file('config/HEADER')
    strictCheck = true
    ignoreFailures = true
    mapping {
        java   = 'SLASHSTAR_STYLE'
        groovy = 'SLASHSTAR_STYLE'
        fxml   = 'XML_STYLE'
    }
    ext.yearCurrent  = new java.text.SimpleDateFormat("yyyy").format(new Date())
    exclude '**/*.svg'
}
